iterm_helpers="${HOME}/.iterm2_shell_integration.zsh"
[[ -f $iterm_helpers ]] && source "$iterm_helpers"

# Keep only the first occurrence of each duplicated value.
# path is the array version of PATH.
typeset -U PATH path

# https://docs.brew.sh/Installation
for homebrew_path in /opt/homebrew /home/linuxbrew/.linuxbrew /usr/local/bin
do
	if [[ -x "$homebrew_path/bin/brew" ]]
	then
		path=("$homebrew_path/bin" "$homebrew_path/sbin" $path)
		break
	fi
done
if command -v brew >/dev/null
then
	homebrew_path=$(brew --prefix)

	# Also load the completion goodies.
	site_functions="$homebrew_path/share/zsh/site-functions"
	if [[ -d $site_functions ]]
	then
		fpath=($site_functions $fpath)
		autoload -U $site_functions/*(:t)
		type _amazon_completions >/dev/null && _amazon_completions
	fi

	# More completions.
	homebrew_complet="$homebrew_path/share/zsh/site-functions"
	if [[ -d $homebrew_complet ]]
	then
		fpath=($fpath /opt/homebrew/share/zsh/site-functions)
	fi
fi
[[ -x "$HOME/.rakubrew/bin/rakubrew" ]] && eval "$("$HOME/.rakubrew/bin/rakubrew" init Zsh)"

# This has higher priority and is installed by Homebrew, i.e. it needs to come after.
if command -v pyenv >/dev/null
then
	eval "$(pyenv init -)"
	command -v py >/dev/null && export PY_PYTHON=$(pyenv global | grep -o '[0-9]\+\.[0-9]\+')
fi
command -v pyenv-virtualenv-init >/dev/null && eval "$(pyenv virtualenv-init -)"

# These have lower priority and are appended.
for append in .local .cargo
do
	[[ -d "$HOME/$append/bin" ]] && path+="$HOME/$append/bin"
done

# https://github.com/antonmedv/walk
function ce {
	cd "$(walk --icons "$@")"
}

gr() {
	cd "$(git rev-parse --show-toplevel)"
}

source "$HOME/.zplug.zsh"

autoload -Uz compinit && compinit

export GITHUB_USERNAME=CmdQ
# https://www.chezmoi.io/
# chezmoi init --apply https://github.com/CmdQ/dotfiles.git
command -v chezmoi >/dev/null && eval "$(chezmoi completion zsh)"

mkcd() {
    if (($# != 1))
	then
        echo 'Provide name of directory(/chain) as the only argument.'
    else
        mkdir -p "$1" && cd "$1" || echo 'Couldn'\''t create directory.'
    fi
}


for f in .zsh_local aliases.sh git.alias amazon.zsh
do
	[[ -f $HOME/$f ]] && source "$HOME/$f"
done
command -v fzf >/dev/null && [[ -r "$HOME/.fzf.zsh" ]] && source "$HOME/.fzf.zsh"

make_aliases() {
	local grep=egrep
	command -v rg >/dev/null && grep='rg --smart-case'
	alias -g NUL="/dev/null"
	alias -g DN=">/dev/null"
	alias -g EN="2>/dev/null"
	alias -g AN="&>/dev/null"
	alias -g G="|$grep"
	alias -g IG="G -i"
	alias -g L="|less -R"
	alias -g GG="2>&1 |$grep"
	alias -g IGG="2>&1 IG"
	alias -g LL="2>&1 |less -R"
}
make_aliases

set -o emacs

autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

bindkey '^[[A' up-line-or-beginning-search
bindkey '^[[B' down-line-or-beginning-search
bindkey '^Q' push-line-or-edit

export ET_NO_TELEMETRY=set
export SAVEHIST=99999
export HISTFILE="$HOME/.zsh_history"
setopt auto_cd
setopt extended_glob
setopt extended_history
setopt hist_expire_dups_first
setopt hist_find_no_dups
setopt hist_ignore_space
setopt hist_no_functions
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt ignore_eof
setopt inc_append_history
setopt interactive_comments
setopt nocasematch
setopt pushd_to_home
setopt share_history

[ -r "$HOME/.config/broot/launcher/bash/br" ] && source "$HOME/.config/broot/launcher/bash/br"
