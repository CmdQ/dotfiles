iterm_helpers="${HOME}/.iterm2_shell_integration.zsh"
[[ -f $iterm_helpers ]] && source "$iterm_helpers"

# Keep only the first occurrence of each duplicated value.
# path is the array version of PATH.
typeset -U PATH path

# Load homebrew completions.
if command -v brew >/dev/null
then
	# Also load the completion goodies.
	site_functions="$homebrew_path/share/zsh/site-functions"
	if [[ -d $site_functions ]]
	then
		fpath=($site_functions $fpath)
		autoload -U $site_functions/*(:t)
		type _amazon_completions >/dev/null && _amazon_completions
	fi

	# More completions.
	homebrew_complet="$homebrew_path/share/zsh/site-functions"
	if [[ -d $homebrew_complet ]]
	then
		fpath=($fpath /opt/homebrew/share/zsh/site-functions)
	fi
fi

# This has higher priority and is installed by Homebrew, i.e. it needs to come after.
if command -v pyenv >/dev/null
then
	eval "$(pyenv init -)"
	command -v py >/dev/null && export PY_PYTHON=$(pyenv global | grep -o '[0-9]\+\.[0-9]\+')
fi
command -v pyenv-virtualenv-init >/dev/null && eval "$(pyenv virtualenv-init -)"

# https://github.com/antonmedv/walk
function ce {
	cd "$(walk --icons "$@")"
}

[ -f "$HOME/.config/broot/launcher/bash/br" ] && source "$HOME/.config/broot/launcher/bash/br"

gr() {
	cd "$(git rev-parse --show-toplevel)"
}

source "$HOME/.zplug.zsh"

autoload -Uz compinit
for dump in "$HOME"/.zcompdump(N.mh+24); do
	compinit
done
compinit -C

# https://www.chezmoi.io/
# chezmoi init --apply https://github.com/CmdQ/dotfiles.git
command -v chezmoi >/dev/null && eval "$(chezmoi completion zsh)"

for f in .zsh_local .aliases.bash git.alias amazon.zsh
do
	[[ -f $HOME/$f ]] && source "$HOME/$f"
done

make_aliases() {
	local grep=egrep
	command -v rg >/dev/null && grep='rg --smart-case'
	alias -g NUL="/dev/null"
	alias -g DN=">/dev/null"
	alias -g EN="2>/dev/null"
	alias -g AN="&>/dev/null"
	alias -g G="|$grep"
	alias -g IG="G -i"
	alias -g L="|less"
	alias -g GG="2>&1 |$grep"
	alias -g IGG="2>&1 IG"
	alias -g LL="2>&1 |less"
}
make_aliases

set -o emacs

autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

bindkey '^[[A' up-line-or-beginning-search
bindkey '^[[B' down-line-or-beginning-search
bindkey '^Q' push-line-or-edit

export SAVEHIST=99999
export HISTFILE="$HOME/.zsh_history"
setopt auto_cd
setopt extended_glob
setopt extended_history
setopt hist_expire_dups_first
setopt hist_find_no_dups
setopt hist_ignore_space
setopt hist_no_functions
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt inc_append_history
setopt interactive_comments
setopt nocasematch
setopt pushd_to_home
setopt share_history
